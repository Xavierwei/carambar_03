<languageVersion: 1.0;>

/***
Xingzhuangzhezhao2
创建人：ZЁЯ¤　身高：168cm+；体重：57kg+；未婚（已有女友）；最爱的运动：睡觉；格言：路见不平，拔腿就跑。QQ：358315553。
创建时间：2012年12月05日 15:34:32
简要说明：这家伙很懒什么都没写。
用法举例：这家伙还是很懒什么都没写。
*/

kernel Xingzhuangzhezhao2
<
	namespace:"zero.shaders.classic";
	vendor:"ZЁЯ¤";
	version:2;
	description:"形状遮罩（环阵）";
>
{
	
	parameter float alpha;
	parameter int style;
	parameter bool isMask;
	parameter float width;
	parameter float height;
	parameter float num;
	parameter float value1;
	parameter float value2;
	parameter float value3;
	parameter bool follow;
	parameter float2 o;
	parameter float tranParam1;
	parameter float tranParam2;
	parameter float tranParam3;
	parameter float tranParam4;
	parameter float tranParam5;
	parameter float tranParam6;
	parameter float tranParam7;
	parameter float tranParam8;
	parameter float tranParam9;

	input image4 src;
	output pixel4 dst;
	
	void evaluatePixel()
	{
		if(alpha>0.0){
			if(width==0.0||height==0.0){
				
				if(isMask){
					dst=float4(0.0,0.0,0.0,0.0);
				}else{
					dst=sampleNearest(src,outCoord());
					dst.a*=alpha;
				}
				
			}else{
				float2 oc=outCoord();
				
				float tranParamKK=tranParam1+tranParam2*oc.x+tranParam3*oc.y;
				float x=(tranParam4+tranParam5*oc.x+tranParam6*oc.y)/tranParamKK;
				float y=(tranParam7+tranParam8*oc.x+tranParam9*oc.y)/-tranParamKK;
				
				if(follow){
					oc.x=x;
					oc.y=y;
				}
				
				x-=o.x;
				y-=o.y;
				
				x*=2.0/width;
				y*=2.0/height;
				
				if(num>1.0){
					
					float radian=atan(y,x);
					
					
					
					float dRad=6.283185307179586/float(num);//每份占的角度
					radian+=1.5707963267948966+dRad*0.5;
					radian-=floor(radian/dRad)*dRad;
					radian-=1.5707963267948966+dRad*0.5;
					
					float len=sqrt(x*x+y*y);
					x=len*cos(radian);
					y=len*sin(radian);
				}
				
				bool draw;
				y=(y+value2)*value3;
				if(style==1){//矩形
					draw=x>-value1&&x<value1&&y>-value1&&y<value1;
				}else if(style==2){
					draw=x+y<value1&&x-y<value1&&-x+y<value1&&-x-y<value1;
				}else if(style==3){//心形
					y-=0.213;
					y*=1.299;
					if(x<0.0){
						x*=-1.0;
					}
					float y_sqrt_x=y+sqrt(x*value1);
					draw=x*x+y_sqrt_x*y_sqrt_x<value1*value1;
				}else{//椭圆
					draw=x*x+y*y<value1*value1;
				}
				
				if(isMask^^draw){
					dst=float4(0.0,0.0,0.0,0.0);
				}else{
					dst=sampleNearest(src,oc);
					dst.a*=alpha;
				}
				
			}
		}else{
			dst=float4(0.0,0.0,0.0,0.0);
		}
	}
}