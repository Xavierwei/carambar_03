<languageVersion: 1.0;>

/***
Yuanqiepian
创建人：ZЁЯ¤　身高：168cm+；体重：57kg+；未婚（已有女友）；最爱的运动：睡觉；格言：路见不平，拔腿就跑。QQ：358315553。
创建时间：2012年12月05日 16:36:37
简要说明：这家伙很懒什么都没写。
用法举例：这家伙还是很懒什么都没写。
*/

kernel Yuanqiepian
<
	namespace:"zero.shaders.classic";
	vendor:"ZЁЯ¤";
	version:2;
	description:"圆切片";
>
{
	
	parameter float alpha;
	parameter float2 center;
	parameter float num;
	parameter float radius;
	parameter float angle;
	parameter float alpha1;
	parameter float alpha2;
	parameter bool showOutside;

	input image4 src;
	output pixel4 dst;
	
	void evaluatePixel()
	{
		if(alpha>0.0){
			float2 oc=outCoord();
			if(radius>0.0){
				float dx=oc.x-center.x;
				float dy=oc.y-center.y;
				float len2=dx*dx+dy*dy;
				if(len2<radius*radius){
					len2=sqrt(len2);
					float radian=(
						angle
						-
						angle/num*floor(len2*(num/radius))
					)*(0.017453292519943295);
					float c=cos(radian);
					float s=sin(radian);
					
					dst=sampleNearest(src,float2(
						center.x+dx*c+dy*s,
						center.y-dx*s+dy*c
					));
					
					dst.a*=alpha*(alpha1+(1.0-alpha1)*len2/radius)*(1.0-(1.0-alpha2)*(len2/radius));
					
				}else{
					if(showOutside){
						dst=sampleNearest(src,oc);
						dst.a*=alpha*alpha2;
					}else{
						dst=float4(0.0,0.0,0.0,0.0);
					}
				}
			}else{
				if(showOutside){
					dst=sampleNearest(src,oc);
					dst.a*=alpha*alpha2;
				}else{
					dst=float4(0.0,0.0,0.0,0.0);
				}
			}
		}else{
			dst=float4(0.0,0.0,0.0,0.0);
		}
	}
}