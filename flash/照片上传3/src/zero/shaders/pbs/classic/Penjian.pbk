<languageVersion: 1.0;>

/***
Penjian
创建人：ZЁЯ¤　身高：168cm+；体重：57kg+；未婚（已有女友）；最爱的运动：睡觉；格言：路见不平，拔腿就跑。QQ：358315553。
创建时间：2012年12月05日 18:02:29
简要说明：这家伙很懒什么都没写。
用法举例：这家伙还是很懒什么都没写。
*/

kernel Penjian
<
	namespace:"zero.shaders.classic";
	vendor:"ZЁЯ¤";
	version:2;
	description:"喷溅";
>
{
	
	parameter float alpha;
	parameter float strength;
	parameter float progress;
	parameter float angle_c;
	parameter float angle_s;
	parameter float2 srcSize
	<
		description:"输入图像的宽高";
		parameterType:"inputSize";
		inputSizeName:"src";
	>;
	

	input image4 src;
	output pixel4 dst;
	
	void evaluatePixel()
	{
		if(alpha>0.0){
			float2 oc=outCoord();
			
			float x=oc.x-srcSize.x*0.5;
			float y=oc.y-srcSize.y*0.5;
			
			float len;
			if(angle_c<0.0){
				len=-srcSize.x*angle_c;
			}else{
				len=srcSize.x*angle_c;
			}
			if(angle_s<0.0){
				len-=srcSize.y*angle_s;
			}else{
				len+=srcSize.y*angle_s;
			}
			float mm=(progress-0.5)*len;
			float dis=mm-(x*angle_c+y*angle_s);
			if(dis<0.0){//位于直线 x*c+y*s=mm “左”侧
				
				float nn=x*angle_s-y*angle_c;
				x=mm*angle_c+nn*angle_s;
				y=mm*angle_s-nn*angle_c;
				dst=sampleNearest(src,float2(
					x+srcSize.x*0.5,
					y+srcSize.y*0.5
				));
				
				oc.x-=x;
				oc.y-=y;
				dst.a*=(1.0-length(oc)/len*(1.0-strength));
				
			}else{
				dst=sampleNearest(src,oc);
			}
			
			dst.a*=alpha;
			
		}else{
			dst=float4(0.0,0.0,0.0,0.0);
		}
	}
}