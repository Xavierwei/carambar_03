<languageVersion: 1.0;>

/***
Wanhuatong
创建人：ZЁЯ¤　身高：168cm+；体重：57kg+；未婚（已有女友）；最爱的运动：睡觉；格言：路见不平，拔腿就跑。QQ：358315553。
创建时间：2012年12月06日 16:40:04
简要说明：这家伙很懒什么都没写。
用法举例：这家伙还是很懒什么都没写。
*/

kernel Wanhuatong
<
	namespace:"zero.shaders.classic";
	vendor:"ZЁЯ¤";
	version:2;
	description:"万花筒";
>
{
	
	parameter float alpha;
	parameter float2 center;
	parameter float num;
	parameter float scale;
	parameter float angle_c;
	parameter float angle_s;
	parameter float2 srcSize
	<
		description:"输入图像的宽高";
		parameterType:"inputSize";
		inputSizeName:"src";
	>;
	

	input image4 src;
	output pixel4 dst;
	
	void evaluatePixel()
	{
		if(alpha>0.0){
			
			float2 oc=outCoord();
			
			float dx=oc.x-center.x;
			float dy=oc.y-center.y;
			
			float radian=atan(dy,dx);
			
			float dRad=6.283185307179586/float(num);//每份占的角度
			radian+=1.5707963267948966+dRad*0.5;
			radian-=floor(radian/dRad)*dRad;
			radian-=1.5707963267948966+dRad*0.5;
			
			float len=sqrt(dx*dx+dy*dy);
			
			dx=len*cos(radian)*scale;
			dy=len*sin(radian)*scale;
			
			dst=sampleNearest(src,float2(
				mod(center.x+(center.x-srcSize.x*0.5)+dx*angle_c+dy*angle_s,srcSize.x),
				mod(center.y+(center.y-srcSize.y*0.5)+dy*angle_c-dx*angle_s,srcSize.y)
			));
			dst.a*=alpha;
			
		}else{
			dst=float4(0.0,0.0,0.0,0.0);
		}
	}
}