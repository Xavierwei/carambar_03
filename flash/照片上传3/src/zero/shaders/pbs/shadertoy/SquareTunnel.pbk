<languageVersion: 1.0;>

/***
SquareTunnel
创建人：ZЁЯ¤　身高：168cm+；体重：57kg+；未婚（已有女友）；最爱的运动：睡觉；格言：路见不平，拔腿就跑。QQ：358315553。
创建时间：2012年12月07日 16:59:59
简要说明：这家伙很懒什么都没写。
用法举例：这家伙还是很懒什么都没写。
*/

kernel SquareTunnel
<
	namespace:"zero.shaders.shadertoy";
	vendor:"ZЁЯ¤";
	version:2;
	description:"Square Tunnel";
>
{
	
	parameter float alpha;
	parameter float time;
	parameter float2 srcSize
	<
		description:"输入图像的宽高";
		parameterType:"inputSize";
		inputSizeName:"src";
	>;
	

	input image4 src;
	output pixel4 dst;
	
	void evaluatePixel()
	{
		if(alpha>0.0){
			
			float2 p =(2.0*(outCoord()/srcSize))-1.0;
			float2 uv=float2(0.0,0.0);
			
			float r = pow( pow(p.x*p.x,16.0) + pow(p.y*p.y,16.0), 1.0/32.0 );
			uv.x = .5*time + 0.5/r;
			uv.y = 1.0*atan(p.y,p.x)/3.1416;
			
			uv.y=1.0-uv.y;
			dst =(sampleNearest(src,mod(uv,1.0)*((srcSize-1.0))))*(r*r*r);
			dst.a=alpha;
			
		}else{
			dst=float4(0.0,0.0,0.0,0.0);
		}
	}
}